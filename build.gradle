// Настройки для всех проектов
allprojects {
    apply plugin: 'idea'
    apply plugin: 'java'
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
        compileOnly 'org.jetbrains:annotations:16.0.2'
    }

    group = 'ru.spliterash'
    version = '3.0'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    jar {
        from {
            def projectClasses = configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
            def subProject = subprojects.collect { p ->
                p.jar.outputs.files.getFiles().collect {
                    it.isDirectory() ? it : zipTree(it)
                }
            }
            return projectClasses + subProject
        }.exclude('META-INF', 'META-INF/**')
    }
}
subprojects {
    dependencies {
        compileOnly parent
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compile 'com.j256.ormlite:ormlite-jdbc:5.1'
    compileOnly 'com.google.code.gson:gson:2.8.6'
    compile 'org.apache.httpcomponents:httpmime:4.5.8'
    compileOnly 'log4j:log4j:1.2.17'
    compileOnly 'org.apache.logging.log4j:log4j-api:2.13.3'
}
/**
 * Копия либ для ProGuard
 */
task copyDependencies(type: Copy) {
    File libFolder = new File(rootProject.getProjectDir(), "libs")
    delete libFolder
    subprojects.each {
        from(it.configurations.compileOnly)
        into libFolder
    }
}